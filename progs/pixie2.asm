;  AN 1802 ANIMATION PROGRAM by E. DEVEAUX
; 
; 
;  THIS PROGRAM PROVIDES VARIABLE SPEED
;  ANIMATION OF THE IMAGE LOCATED AT #78 to
;  #F7 IN MEMORY.
;  SPEED CONTROL IS PROVIDED BY INPUT

; enable only one of these:
xroll	equ	0
xshift	equ	1
xboth	equ	0

	GHI	0	;ZERO HIGH ORDER OF
	PHI	1	;R1 R2 R3.
	PHI	2
	PHI	3
	PHI 	4	;R4 POINTS TO REFRESH
	PLO	4	;ADDRESS
	LDI	INTRPT.0
	PLO	1
	LDI	STACK.0
	PLO	2
	LDI	MAIN.0
	PLO	3
	SEP	3	;GO TO MAIN_LINE
	db	1,2,3,0	;STACK AREA
STACK	equ	$-1
; 
; THIS PROGRAM USES A MODIFIED VERSION
; OF THE INTERRUPT ROUTINE THAT APPEARED
; IN COSMAC ELF PART 4.
; 
; A SHIFT ROUTINE HAS BEEN ADDED THAT MOVES THE
;  STARSHIP FROM LEFT TO RIGHT ACROSS THE CRT.
; 
RETURN:	LDXA
	RET		;CYCLES
INTRPT:	DEC	2	;2
	SAV		;4R5 COUNTS REFRESH
	DEC	2	;6	CYCLES, USED TO
	STR	2	;8	DETERMINE WHEN TO
	INC	5	;10	SHIFT /ROLL.
	NOP		;13
	GHI	4	;15	R4 TO R0
	PHI	0	;17	REFRESH ADDRESS
	GLO	4	;19
	PLO	0	;21

	GLO	0	;23
	GLO	0	;25
REFRESH:
	GLO	0	; 27
	SEX	2	; 29
	;	8 DMA CYCLES
	SEX	2
	DEC	0
	PLO	0
	;	8 DMA CYCLES
	SEX	2
	DEC	0
	PLO	0
	;	8 DMA CYCLES
	SEX	2
	DEC	0
	PLO	0
	;	8 DMA CYCLES
	BN1	REFRESH	; ON EF1 REFRESH
	BR	RETURN	; IS OVER.

MAIN:	SEX	2	; RX=2
	INP	1	; TELL 1861 TO
			; TURN ON CRT.
; SFREAD READS INPUT SWITCHESTO CONTROL
; SPEED OF SHIFTS/ROLLS.
; INPUT SWITCH IS STORED AT STACK M(R2).
; 
; INITIAL VALUE OF STACK IS ZERO AND THERE IS
; NO MOVEMENT OF STAR SHIP UNTIL A NON ZERO BIT
; IS INPUT.
SFREAD:	BN4	CKSHIF	; IF NO INPUT GO
WTREAD:	B4	WTREAD	; IF TIME TO SHIFT.
	INP	4	; READ INTO STACK.
CKSHIF:	GLO	5	; GHI R5 VARY/SPEED
	AND		; OF STAR SHIP.
	BZ	SFREAD	; SHIFT/ROLL BIT MATCH.
 if xroll
	BR	ROLL	; roll only
 else
	LDI	BEGSFT.1	;BR ROLL 3061
 endi
	PHI	9	;ROLL NO SHIFT


	LDI	BEGSFT.0
	PLO	9	;R9=FIRST LINE
	LDI	16	;TO SHIFT.
	PLO	6	;SHIFT 16 LINES.
NXTLNE:	GHI	9
	PHI	10	;SAVE ADDRESS OF 1st
	GLO	9	;ON LINE IN RA
	PLO	10
	LDI	7	;R7=BYTES TO SHIFT-1.
	PLO	7
	LDN	9
	PHI	8	;SAVE 1ST BYTE ON
	SHRC		;LINE IN R8.1
NXTBYT:	INC	9	;POINT R9 TO NEXT BYTE.
	LDN	9	;LOAD NEXT BYTE.
	SHRC		;SHIFT RIGHT.
	STR	9	;STORE BYTE
	DEC	7
	GLO	7	;CHECK IF ALL BYTES
	BNZ	NXTBYT	;SHIFTED.
	GHI	8	;PUT BIT 0 of 8TH
	SHRC		;BYT ON BIT 7 OF
	STR	10	;1ST BYT ON LINE.
	INC	9	;R9=BYTE 0 NXT LINE.
	DEC	6
	GLO	6	;CHECK IF 16 LINES
	BNZ	NXTLNE	;SHIFTED.
; enable only one of these:
 if xboth
	SKP		; both shift and roll
	db	SFREAD.0
 endi
 if xshift
	BR	SFREAD	;SKP 38 ROLL AND SHIFT.
 endi
ROLL:	GLO	4	;INCREMENT R4 ONE LINE
	ADI	8	;ROLL SCREEN UP.
	PLO	4
	GHI	4	;CHANGE LNNO 116 TO
	LDI	00	;ADCI 0 7C00 IF MORE
	PHI	4	;THAN 256 BYTES.
	BZ	SFREAD
	GLO	4
	PHI	4
	BR	SFREAD

; ENTER IMAGE TO BE SHIFTED IN LOCATIONS
; X'78' - x'F7'.
	;org	($ + 7) and 0fff8h	; next CRT scanline
	fill	0, (($ + 7) and 0fff8h) - $
	db	0,0,0,0,0,0,0,0	; force blank line...
BEGSFT:	ds	0
 if 0	; no space for the "COSMAC" lines...
	db	7Bh,0DEh,0DBh,0DEh,000h,000h,000h,000h
	db	4Ah,050h,0DAh,052h,000h,000h,000h,000h
	db	42h,05Eh,0ABh,0D0h,000h,000h,000h,000h
	db	4Ah,042h,08Ah,052h,000h,000h,000h,000h
	db	7Bh,0DEh,08Ah,05Eh,000h,000h,000h,000h
	db	0,0,0,0,0,0,0,0
 endi
	db	0,0,0,0,0,0,7,0e0h
	db	00h,000h,000h,000h,0FFh,0FFh,0FFh,0FFh
	db	00h,006h,000h,001h,000h,000h,000h,001h
	db	00h,07Fh,0E0h,001h,000h,000h,000h,002h
	db	7Fh,0C0h,03Fh,0E0h,0FCh,0FFh,0FFh,0FEh
	db	40h,00Fh,000h,010h,004h,080h,000h,000h
	db	7Fh,0C0h,03Fh,0E0h,004h,080h,000h,000h
	db	00h,03Fh,0D0h,040h,004h,080h,000h,000h
	db	00h,00Fh,008h,020h,004h,080h,000h,000h
	db	00h,000h,007h,090h,004h,080h,000h,000h
	db	00h,000h,018h,07Fh,0FCh,0F0h,000h,000h
	db	00h,000h,030h,000h,000h,010h,000h,000h
	db	00h,000h,073h,0FCh,000h,010h,000h,000h
	db	00h,000h,030h,000h,03Fh,0F0h,000h,000h
	db	00h,000h,018h,00Fh,0C0h,000h,000h,000h
	db	00h,000h,007h,0F0h,000h,000h,000h,000h

	END
